<program> ::= <function_list>

<function_list> ::= <function> <function_list>
<function_list> ::= ε

<function> ::= <type> IDENTIFIER LPAREN <param_list> RPAREN LBRACE <stmt_list> RBRACE

<param_list> ::= <param> <param_list_tail>
<param_list> ::= ε

<param_list_tail> ::= COMMA <param> <param_list_tail>
<param_list_tail> ::= ε

<param> ::= <type> IDENTIFIER

<type> ::= INT
<type> ::= VOID

<stmt_list> ::= <stmt> <stmt_list>
<stmt_list> ::= ε

<stmt> ::= <var_decl>          #var_decl_code
<stmt> ::= <assign_stmt>       #assign_code
<stmt> ::= <if_stmt>           #if_code
<stmt> ::= <while_stmt>        #while_code
<stmt> ::= <return_stmt>       #return_code
<stmt> ::= <pure_expr_stmt>    #expr_code
<stmt> ::= <block_stmt>        #block_code

<var_decl> ::= <type> IDENTIFIER SEMI
<assign_stmt> ::= SET IDENTIFIER ASSIGN <expr> SEMI

<if_stmt> ::= IF LPAREN <expr> RPAREN <stmt> <if_tail>
<if_tail> ::= ELSE <stmt>
<if_tail> ::= ε

<while_stmt> ::= WHILE LPAREN <expr> RPAREN <stmt>

<return_stmt> ::= RETURN <return_tail>
<return_tail> ::= <expr> SEMI
<return_tail> ::= SEMI

<pure_expr_stmt> ::= <expr> SEMI
<block_stmt> ::= LBRACE <stmt_list> RBRACE

<expr> ::= <add_expr> <rel_tail>


<rel_tail> ::= LT <add_expr>
<rel_tail> ::= GT <add_expr>
<rel_tail> ::= LE <add_expr>
<rel_tail> ::= GE <add_expr>
<rel_tail> ::= EQ <add_expr>
<rel_tail> ::= NE <add_expr>
<rel_tail> ::= ε

<add_expr> ::= <mul_expr> <add_tail>


<add_tail> ::= PLUS <mul_expr> <add_tail>
<add_tail> ::= MINUS <mul_expr> <add_tail>
<add_tail> ::= ε

<mul_expr> ::= <factor> <mul_tail>


<mul_tail> ::= MULT <factor> <mul_tail>
<mul_tail> ::= DIV <factor> <mul_tail>
<mul_tail> ::= ε


<factor> ::= NUMBER
<factor> ::= IDENTIFIER
<factor> ::= LPAREN <expr> RPAREN